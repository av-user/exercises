	AREA max7219_utilities, CODE, READONLY

max7219_init	PROC
	EXPORT	max7219_init
	;R0: MAX7219 control block address
	PUSH	{LR,R4}
	MOV		R4,	R0
	LDR		R0, =DELAY_MCS
	BL		nrf_delay_mcs
	MOVS	R1, #MAX7219CB_GPIO_DIN_OFFSET
	LDRB	R0, [R4,R1]
	BL		gpio_config_output
	LDR		R0, =DELAY_MCS
	BL		nrf_delay_mcs
	MOVS	R1, #MAX7219CB_GPIO_CS_OFFSET
	LDRB	R0, [R4,R1]
	BL		gpio_config_output
	LDR		R0, =DELAY_MCS
	BL		nrf_delay_mcs
	MOVS	R1, #MAX7219CB_GPIO_CLK_OFFSET
	LDRB	R0, [R4,R1]
	BL		gpio_config_output
	LDR		R0, =DELAY_MCS
	BL		nrf_delay_mcs
	MOVS	R1, #MAX7219CB_GPIO_CS_OFFSET
	LDRB	R0, [R4,R1]
	BL		gpio_set_high
	;MOVS	R0, #MAX7219CB_LED_BUF_OFFSET	;commented out because assumed ititialized with zeros
	;MOVS	R1, #MAX7219CB_GPIO_DIN_OFFSET
	;BL		zero_mem
	MOV		R0,	R4
	MOVS	R1, #MAX7219CB_OP_DISPLAY_TEST
	MOVS	R2, #MAX7219CB_DSPLTST_NORMAL
	;MOVS	R2, #MAX7219CB_DSPLTST_TESTMODE
	BL		max7219_send
	MOV		R0,	R4
	MOVS	R1, #MAX7219CB_OP_SCAN_LIMIT
	MOVS	R2, #MAX7219CB_SCAN_LIMIT_OFFSET
	LDRB	R2, [R4,R2]
	BL		max7219_send
	MOV		R0,	R4
	MOVS	R1, #MAX7219CB_OP_DECODE_MODE
	MOVS	R2, #MAX7219CB_DECODE_MODE_OFFSET
	LDRB	R2, [R4,R2]
	BL		max7219_send
	MOV		R0,	R4
	BL		max7219_clear_display
	MOV		R0,	R4
	MOVS	R1, #MAX7219CB_OP_SHUTDOWN
	MOVS	R2, #MAX7219CB_SHUTDOWN_SHUTDOWNMODE
	BL		max7219_send
	MOV		R0,	R4
	MOVS	R1, #MAX7219CB_INTENSITY_OFFSET
	LDRB	R1, [R4,R1]
	BL		max7219_set_intensity
	EORS	R0,R0,R0
	POP		{PC,R4}
    ENDP

max7219_set_intensity	PROC
	EXPORT	max7219_set_intensity	
	;R0: control block address
	;R1: intensity
	PUSH	{LR}
	MOV		R2, R1
	MOVS	R1, #MAX7219CB_OP_INTENSITY
	BL		max7219_send
	POP		{PC}
    ENDP

	ROUT
max7219_set_matrix	PROC
	EXPORT	max7219_set_matrix
	;R0: control block address
	;R1: matrix data address (8 bytes)
	PUSH	{LR,R4-R7}
	MOV		R4, R0
	MOV		R5, R1
	MOVS	R6, #8
	MOVS	R7, #MAX7219CB_OP_DIGIT0
0 	NOP
	MOV		R0, R4
	MOV		R1, R7
	LDRB	R2, [R5]
	BL		max7219_send
	ADDS	R5, #0x01	;next matrix data byte
	ADDS	R7, #0x01	;next digit (MAX7219CB_OP_DIGIT0...7
	SUBS	R6, #0x01
	BNE		%0
	POP		{PC,R4-R7}
    ENDP

max7219_wakeup	PROC
	EXPORT	max7219_wakeup
	;R0: control block address
	PUSH	{LR}
	MOVS	R1, #MAX7219CB_OP_SHUTDOWN
	MOVS	R2, #MAX7219CB_SHUTDOWN_NORMAL_OPERATION
	BL		max7219_send
	POP		{PC}
    ENDP

	ROUT
max7219_send	PROC
	;R0: control block address
	;R1: register address
	;R2: value
	PUSH	{LR,R4-R6}
	MOV		R4, R0
	MOV		R5, R1
	MOV		R6, R2
	MOVS	R1, #MAX7219CB_GPIO_CS_OFFSET
	LDRB	R0, [R4,R1]
	BL		gpio_set_low
	LDR		R0, =DELAY_CLK_MCS
	BL		nrf_delay_mcs
	MOV		R0, R4
	MOV		R1, R5
	BL		shift_out
	MOV		R0, R4
	MOV		R1, R6
	BL		shift_out
	LDR		R0, =DELAY_CLK_MCS
	BL		nrf_delay_mcs
	MOVS	R1, #MAX7219CB_GPIO_CS_OFFSET
	LDRB	R0, [R4,R1]
	BL		gpio_set_high
	POP		{PC,R4-R6}
    ENDP

	ROUT
shift_out	PROC
	;R0: control block address
	;R1: data to be shifted
	PUSH	{LR,R4-R7}
	MOV		R4, R1
	;we need cb address only to get din & clk gpio numbers. get it and we may forget cb address
	MOVS	R1, #MAX7219CB_GPIO_DIN_OFFSET
	LDRB	R6, [R0,R1]
	MOVS	R1, #MAX7219CB_GPIO_CLK_OFFSET
	LDRB	R7, [R0,R1]
	;shift lowest byte to the left
	LSLS	R4, #24
	MOVS	R5,	#0x08
0	MOV		R0, R7
	BL		gpio_set_low
	LDR		R0, =DELAY_CLK_MCS
	BL		nrf_delay_mcs
	MOV		R0, R6
	LSLS	R4, #0x01
	BCS		%1
	BL		gpio_set_low
	B		%2
1	BL		gpio_set_high
2	LDR		R0, =DELAY_CLK_MCS
	BL		nrf_delay_mcs
	MOV		R0, R7
	BL		gpio_set_high
	LDR		R0, =DELAY_CLK_MCS
	BL		nrf_delay_mcs
	SUBS	R5, #0x01
	BNE		%0
	POP		{PC,R4-R7}
    ENDP

	ROUT
max7219_clear_display	PROC
	PUSH	{LR,R4}
	;R0: control block address
	MOV		R4, R0
	ADDS	R0, #MAX7219CB_LED_BUF_OFFSET
	MOV		R1, R4
	ADDS	R1, #MAX7219CB_GPIO_DIN_OFFSET
	BL		zero_mem
	MOV		R0, R4
	MOVS	R1, #MAX7219CB_OP_DIGIT0
	EORS	R2,R2,R2
	BL		max7219_send
	MOV		R0, R4
	MOVS	R1, #MAX7219CB_OP_DIGIT1
	EORS	R2,R2,R2
	BL		max7219_send
	MOV		R0, R4
	MOVS	R1, #MAX7219CB_OP_DIGIT2
	EORS	R2,R2,R2
	BL		max7219_send
	MOV		R0, R4
	MOVS	R1, #MAX7219CB_OP_DIGIT3
	EORS	R2,R2,R2
	BL		max7219_send
	MOV		R0, R4
	MOVS	R1, #MAX7219CB_OP_DIGIT4
	EORS	R2,R2,R2
	BL		max7219_send
	MOV		R0, R4
	MOVS	R1, #MAX7219CB_OP_DIGIT5
	EORS	R2,R2,R2
	BL		max7219_send
	MOV		R0, R4
	MOVS	R1, #MAX7219CB_OP_DIGIT6
	EORS	R2,R2,R2
	BL		max7219_send
	MOV		R0, R4
	MOVS	R1, #MAX7219CB_OP_DIGIT7
	EORS	R2,R2,R2
	BL		max7219_send
	POP		{PC,R4}
    ENDP

;******************

;max7219 control block structure
MAX7219CB_LED_BUF_OFFSET			EQU	0x00
MAX7219CB_GPIO_DIN_OFFSET			EQU	0x08
MAX7219CB_GPIO_CS_OFFSET			EQU	0x09
MAX7219CB_GPIO_CLK_OFFSET			EQU	0x0A
MAX7219CB_SCAN_LIMIT_OFFSET			EQU	0x0B
MAX7219CB_DECODE_MODE_OFFSET		EQU	0x0C
MAX7219CB_INTENSITY_OFFSET			EQU	0x0D

;register address map
MAX7219CB_OP_NOOP			EQU	0x00
MAX7219CB_OP_DIGIT0			EQU	0x01
MAX7219CB_OP_DIGIT1			EQU	0x02
MAX7219CB_OP_DIGIT2			EQU	0x03
MAX7219CB_OP_DIGIT3			EQU	0x04
MAX7219CB_OP_DIGIT4			EQU	0x05
MAX7219CB_OP_DIGIT5			EQU	0x06
MAX7219CB_OP_DIGIT6			EQU	0x07
MAX7219CB_OP_DIGIT7			EQU	0x08
MAX7219CB_OP_DECODE_MODE	EQU	0x09
MAX7219CB_OP_INTENSITY		EQU	0x0A
MAX7219CB_OP_SCAN_LIMIT		EQU	0x0B
MAX7219CB_OP_SHUTDOWN		EQU	0x0C
MAX7219CB_OP_DISPLAY_TEST	EQU	0x0F

	EXPORT	MAX7219CB_DSPLTST_NORMAL
	EXPORT	MAX7219CB_DSPLTST_TESTMODE
	EXPORT	MAX7219CB_SCANLIMIT_1_DIGIT
	EXPORT	MAX7219CB_SCANLIMIT_2_DIGITS
	EXPORT	MAX7219CB_SCANLIMIT_3_DIGITS
	EXPORT	MAX7219CB_SCANLIMIT_4_DIGITS
	EXPORT	MAX7219CB_SCANLIMIT_5_DIGITS
	EXPORT	MAX7219CB_SCANLIMIT_6_DIGITS
	EXPORT	MAX7219CB_SCANLIMIT_7_DIGITS
	EXPORT	MAX7219CB_SCANLIMIT_8_DIGITS
MAX7219CB_DSPLTST_NORMAL		EQU	0x00
MAX7219CB_DSPLTST_TESTMODE		EQU	0x01
MAX7219CB_SCANLIMIT_1_DIGIT		EQU	0x00
MAX7219CB_SCANLIMIT_2_DIGITS	EQU	0x01
MAX7219CB_SCANLIMIT_3_DIGITS	EQU	0x02
MAX7219CB_SCANLIMIT_4_DIGITS	EQU	0x03
MAX7219CB_SCANLIMIT_5_DIGITS	EQU	0x04
MAX7219CB_SCANLIMIT_6_DIGITS	EQU	0x05
MAX7219CB_SCANLIMIT_7_DIGITS	EQU	0x06
MAX7219CB_SCANLIMIT_8_DIGITS	EQU	0x07

	EXPORT	MAX7219CB_DECMODE_NODECODE
	EXPORT	MAX7219CB_DECMODE_B0_NO71
	EXPORT	MAX7219CB_DECMODE_B30_NO74
	EXPORT	MAX7219CB_DECMODE_B70
MAX7219CB_DECMODE_NODECODE	EQU	0x00
MAX7219CB_DECMODE_B0_NO71	EQU	0x01
MAX7219CB_DECMODE_B30_NO74	EQU	0x0F
MAX7219CB_DECMODE_B70		EQU	0xFF

	EXPORT	MAX7219CB_INTENSITY_0_of_F
	EXPORT	MAX7219CB_INTENSITY_1_of_F
	EXPORT	MAX7219CB_INTENSITY_2_of_F
	EXPORT	MAX7219CB_INTENSITY_3_of_F
	EXPORT	MAX7219CB_INTENSITY_4_of_F
	EXPORT	MAX7219CB_INTENSITY_5_of_F
	EXPORT	MAX7219CB_INTENSITY_6_of_F
	EXPORT	MAX7219CB_INTENSITY_7_of_F
	EXPORT	MAX7219CB_INTENSITY_8_of_F
	EXPORT	MAX7219CB_INTENSITY_9_of_F
	EXPORT	MAX7219CB_INTENSITY_A_of_F
	EXPORT	MAX7219CB_INTENSITY_B_of_F
	EXPORT	MAX7219CB_INTENSITY_C_of_F
	EXPORT	MAX7219CB_INTENSITY_D_of_F
	EXPORT	MAX7219CB_INTENSITY_E_of_F
	EXPORT	MAX7219CB_INTENSITY_F_of_F
MAX7219CB_INTENSITY_0_of_F	EQU	0x00
MAX7219CB_INTENSITY_1_of_F	EQU	0x01
MAX7219CB_INTENSITY_2_of_F	EQU	0x02
MAX7219CB_INTENSITY_3_of_F	EQU	0x03
MAX7219CB_INTENSITY_4_of_F	EQU	0x04
MAX7219CB_INTENSITY_5_of_F	EQU	0x05
MAX7219CB_INTENSITY_6_of_F	EQU	0x06
MAX7219CB_INTENSITY_7_of_F	EQU	0x07
MAX7219CB_INTENSITY_8_of_F	EQU	0x08
MAX7219CB_INTENSITY_9_of_F	EQU	0x09
MAX7219CB_INTENSITY_A_of_F	EQU	0x0A
MAX7219CB_INTENSITY_B_of_F	EQU	0x0B
MAX7219CB_INTENSITY_C_of_F	EQU	0x0C
MAX7219CB_INTENSITY_D_of_F	EQU	0x0D
MAX7219CB_INTENSITY_E_of_F	EQU	0x0E
MAX7219CB_INTENSITY_F_of_F	EQU	0x0F

MAX7219CB_SHUTDOWN_SHUTDOWNMODE		EQU	0X00
MAX7219CB_SHUTDOWN_NORMAL_OPERATION	EQU	0X01

	EXTERN	gpio_set_high
	EXTERN	gpio_set_low
	EXTERN	gpio_config_output

	EXTERN	nrf_delay_mcs
	EXTERN	zero_mem

DELAY_MCS			EQU		7000
DELAY_CLK_MCS		EQU		1

	END
